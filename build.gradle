/*
 * Copyright 2017 Google, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 *
 */

buildscript {
    ext.versions = [
            'android_gradle' : '3.2.0-rc03',
            'androidx'       : [
                    'appcompat'        : '1.0.0-rc02',
                    'browser'          : '1.0.0-rc02',
                    'core'             : '2.0.0-beta01',
                    'constraint_layout': '2.0.0-alpha2',
                    'core_ktx'         : '1.0.0-rc02',
                    'fragment'         : '1.0.0-rc02',
                    'emoji'            : '1.0.0-rc02',
                    'lifecycle'        : '2.0.0-rc01',
                    'paging'           : '2.0.0-rc01',
                    'palette'          : '1.0.0-rc02',
                    'recyclerview'     : '1.0.0-rc02',
                    'room'             : '2.0.0-rc01',
                    'test_runner'      : '1.1.0-alpha4',
                    'test_rules'       : '1.1.0-alpha4',
                    'work'             : '1.0.0-alpha08'
            ],
            'coroutines'     : '0.26.0',
            'dagger'         : '2.17',
            'epoxy'          : '2.16.4',
            'espresso'       : '3.1.0-alpha4',
            'firebase'       : '16.0.3',
            'glide'          : '4.8.0',
            'kotlin'         : '1.2.61',
            'ktlint'         : '0.19.0',
            'mockito'        : '2.18.3',
            'material_design': '1.0.0-rc02',
            'rx'             : '2.2.1',
            'rxkotlin'       : '2.3.0',
            'rxlint'         : '1.6.1',
            'threetenbp'     : '1.3.7',
            'threetenabp'    : '1.1.0',
            'retrofit'       : '2.4.0',
    ]

    ext.buildConfig = [
            'compileSdk': 28,
            'minSdk'    : 23,
            'targetSdk' : 28,
    ]

    repositories {
        google()
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }

        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:${versions.android_gradle}"

        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:${versions.kotlin}"

        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.8.3'

        classpath 'com.github.triplet.gradle:play-publisher:1.2.2'

        classpath 'com.google.gms:google-services:4.1.0'
        classpath 'io.fabric.tools:gradle:1.25.4'
    }
}

plugins {
    id "com.diffplug.gradle.spotless" version "3.14.0"
    id 'com.github.ben-manes.versions' version "0.20.0"
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
}

subprojects {
    apply plugin: 'com.diffplug.gradle.spotless'
    spotless {
        kotlin {
            target "**/*.kt"
            ktlint(versions.ktlint)
            licenseHeaderFile project.rootProject.file('spotless/copyright.kt')
        }
    }
}

Object propOrDef(String propertyName, Object defaultValue) {
    def propertyValue = project.properties[propertyName]
    return propertyValue != null ? propertyValue : defaultValue
}
